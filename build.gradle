buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework:springloaded:1.2.8.RELEASE'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'jacoco'
    id 'java'
    id 'idea'
    id 'org.sonarqube' version '2.6.1'
    id 'org.springframework.boot' version '2.2.4.RELEASE'
}

clean {
    delete 'data'
}

license {
    ignoreFailures true
    header = file('LICENSE')
}

jar {
    archiveBaseName = 'pivio-server'
    archiveVersion = '1.1.0'
}

repositories {
    jcenter()
}

dependencies {
    implementation 'com.flipkart.zjsonpatch:zjsonpatch:0.4.2'
    implementation 'org.apache.commons:commons-lang3:3.7'
    // compile 'org.elasticsearch.plugin:delete-by-query:' + project.properties["dep.elasticsearch"]
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.2.4.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch:2.2.4.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.2.4.RELEASE'

    testImplementation 'com.jayway.jsonpath:json-path'
    testImplementation 'org.apache.commons:commons-text:1.2'
    testImplementation 'org.assertj:assertj-core:3.9.0'
    testImplementation 'org.awaitility:awaitility:3.0.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.4.RELEASE'
    testImplementation 'org.testcontainers:testcontainers:1.5.1'
}

// see https://github.com/ben-manes/gradle-versions-plugin
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

tasks.withType(Test) {
    systemProperty 'gradleIsRunning', true
}